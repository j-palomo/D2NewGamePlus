Class PlasmaShocktrooper : Actor replaces Shocktrooper
{
	Bool WasGibbed;
	Override void PostBeginPlay()
	{
		WasGibbed = False;
	}
	Default
	{
		Monster;
		Health 100;
		Speed 10;
		Radius 20;
		Height 56;
		ReactionTime 8;
		Painchance 30;
		Mass 100;
		+FLOORCLIP;
		ActiveSound "shocktrooper/active";
		PainSound "shocktrooper/pain";
		DeathSound "shocktrooper/death";
		GibHealth 30; // We almost never get -100 health unless hit with a rocket/bfgball at really low health
		Tag "$TAG_SHOCKTROOPER";
		Obituary "$OB_SHOCKTROOPER";
		DropItem "Cell";
	}
	
	States
	{
		Spawn:
			PPOS AB 10 A_Look;
			Loop;
		
		FastSee:
			PPOS AABBCCDD 2 Fast A_FastChase();
			Goto See;
		See:
			PPOS AABBCCDD 2 Fast A_Chase();
			Loop;
			
		Missile:
			PPOS E 10 A_FaceTarget;
			PPOS F 2 Fast A_SpawnProjectile("PlasmaBall");
			PPOS E 4 Fast;
			PPOS F 2 Fast A_SpawnProjectile("PlasmaBall");
			PPOS E 4 Fast;
			PPOS F 2 Fast A_SpawnProjectile("PlasmaBall");
			PPOS E 4 Fast;
			Goto See;
			
		Pain:
			PPOS G 5 Fast;
			PPOS G 5 Fast A_Pain();
			Goto FastSee;
			
		Death:
			PPOS H 0 A_FaceTarget();
			PPOS H 5 A_SpawnItemEx ("DeadShocktrooperHead", 0, 0, 40.0, -2.0, 0, 1.5, 0, SXF_SETMASTER);
			PPOS I 5 A_Scream();
			PPOS J 5 A_Fall();
			PPOS KL 5;
			PPOS M -1;
			Stop;
			
		XDeath:
			PPOS N 5 {WasGibbed = True;}
			PPOS O 5 A_XScream();
			PPOS P 5 A_Fall();
			PPOS Q 0 A_FaceTarget();
			PPOS Q 5 A_SpawnItemEx ("DeadShocktrooperTorso", -8.0, 0, 32.0, -4.0, 0, 2.0, 0, SXF_SETMASTER);
			PPOS RST 5;
			PPOS U -1;
			Stop;
			
		Raise:
			---- A 5 {
				A_RemoveChildren (true, RMVF_EVERYTHING, "DeadShocktrooperHead");
				A_RemoveChildren (true, RMVF_EVERYTHING, "DeadShocktrooperTorso");
				If (WasGibbed)
				{
					WasGibbed = False;
					Return ResolveState ("GibbedRaise");
				}
				Return ResolveState ("NormalRaise");
			}
			Goto NormalRaise;
		
		NormalRaise:
			PPOS LKJIH 5;
			Goto See;
		
		GibbedRaise:
			PPOS TSRQPON 5;
			Goto See;
	}
}

Class DeadShocktrooperHead : Actor
{
	Default
	{
		Damage 0;
		Speed 8;
		Radius 6;
		Height 16;
		-SOLID;
		+MISSILE;
		+DROPOFF;
		BounceType "Doom";
		BounceFactor 0.5;
		+BOUNCEONACTORS;
		+CANBOUNCEWATER;
		+BOUNCEAUTOOFFFLOORONLY;
		Gravity 0.125;
		BounceSound "shocktrooper/head";
		DeathSound "shocktrooper/head";
	}
	
	States
	{
		Spawn:
			PHED ABCDEFGHI 3 A_SpawnParticle ("CA0000", 0, 35, 4.0, 0, Random (-5.0, 5.0), Random (-5.0, 5.0), Random (-1.0, 1.0), Random (-2.0, 2.0), Random (-5.0, 5.0), Random (-4.0, 1.0), 0, 0, -0.125, 1.0, 0);
			Loop;
		Death:
			PHED J -1 A_QueueCorpse; // Limit this for performance reasons
			Loop;
	}
}

Class DeadShocktrooperTorso : Actor
{
	Default
	{
		Damage 0;
		Speed 8;
		Radius 6;
		Height 16;
		+NOBLOCKMAP;
		+DROPOFF;
		+MISSILE;
	}
	
	States
	{
		Spawn:
			PPOS V -1;
			Loop;
		Death:
			PPOS W 5;
			PPOS X -1 A_QueueCorpse; // Limit this for performance reasons
			Stop;
	}
}
