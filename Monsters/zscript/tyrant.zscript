Class MiniCyberdemon : Actor replaces Tyrant
{
	Default
	{
		Monster;
		Health 1000;
		Speed 16;
		Radius 40;
		Height 110;
		Mass 1000;
		PainChance 20;
		MinMissileChance  160;
		+MISSILEMORE;
		+FLOORCLIP;
		+BOSSDEATH;
		+E4M6BOSS;
		+E2M8BOSS;
		Species "Tyrant";
		Tag "$TAG_TYRANT";
		Obituary "$OB_TYRANT";
		SeeSound "tyrant/sight";
		ActiveSound "tyrant/active";
		PainSound "tyrant/pain";
		DeathSound "tyrant/death";
		DropItem "RocketAmmo";
	}
	
	States
	{
		Spawn:
			CYB2 AB 10 A_Look ();
			Loop;
			
		See:
			CYB2 A 3 A_Hoof ();
			CYB2 ABBCC 3 A_Chase ();
			CYB2 D 0 A_StartSound ("tyrant/walk");
			CYB2 DD 3 A_Chase ();
			Loop;
		
		Missile:
			CYB2 E 6 A_FaceTarget ();
			CYB2 F 12 Bright A_TyrantAttack ();
			CYB2 E 12 A_FaceTarget ();
			CYB2 F 12 Bright A_TyrantAttack ();
			CYB2 E 12 A_FaceTarget ();
			CYB2 F 12 Bright A_TyrantAttack ();
			Goto See;
			
		Pain:
			CYB2 G 10 A_Pain ();
			Goto See;
			
		Death:
			CYB2 H 10;
			CYB2 I 10 A_Scream ();
			CYB2 JKL 10;
			CYB2 M 10 A_Fall ();
			CYB2 NO 10;
			CYB2 P 30;
			CYB2 P -1 A_BossDeath ();
			Stop;
			
		Raise:
			CYB2 PONMLKJIH 5;
			Goto See;
	}
	
	Action void A_TyrantAttack ()
	{
		If (target)
		{
			A_FaceTarget ();
			A_SpawnProjectile ("TyrantRocket", 48.0, -24.0);
		}
	}
}

Class TyrantBoss_1 : MiniCyberdemon replaces TyrantBoss1 {}

Class TyrantBoss_2 : MiniCyberdemon replaces TyrantBoss2 {}
