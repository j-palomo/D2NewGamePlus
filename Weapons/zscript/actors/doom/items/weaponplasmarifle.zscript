Class RebalancedPlasmaRifle : PlasmaRifle replaces PlasmaRifle
{
	Default { Weapon.SlotNumber 6; }
	States
	{
		Fire:
			PLSG A 2 A_FireBalancedPlasmaRifle;
			PLSG A 1 {
				A_ZoomFactor (1.0);
				A_WeaponOffset (0, 32, WOF_INTERPOLATE);
			}
			PLSG B 20 A_ReFire;
			Goto Ready;
		FlashA:
			PLSF A 3 Bright A_Light1;
			Goto LightDone;
		FlashB:
			PLSF B 3 Bright A_Light1;
			Goto LightDone;
		
	}
	
	Action void A_FireBalancedPlasmaRifle ()
	{
		A_FireProjectile ("PlayerPlasma");
		If (Random (0,1)) A_GunFlash ("FlashB");
		Else A_GunFlash ("FlashA");
		A_ZoomFactor (0.99);
		A_Quake (1.5, 3, 0, 1, "");
		A_WeaponOffset (Random (-1, 1), Random (32, 35), WOF_INTERPOLATE);
	}
}

Class PlayerPlasma : PlasmaBall
{
	Default
	{
		Radius 8; // Smaller projectiles
		Height 8;
		Speed 28; // Slightly faster
 		DamageFunction ((Random(1, 3) * 5) + 15); // Slightly less randomized damage. Original 5-40, now 20-30
		DamageType "Plasma";
		Projectile;
		+ZDOOMTRANS;
		RenderStyle "Add";
		Alpha 0.75;
		SeeSound "weapons/plasmaf";
		DeathSound "weapons/plasmax";
		Obituary "$OB_MPPLASMARIFLE";
		Decal "PlasmaScorch";
	}
	
	States
	{
		Spawn:
			PLSS AB 3 Bright Light ("PLASMABALL");
			Loop;
		Death:
			PLSE A 4 Bright Light ("PLASMA_X1");
			PLSE BC 4 Bright Light ("PLASMA_X2");
			PLSE D 4 Bright Light ("PLASMA_X3");
			PLSE E 4 Bright Light ("PLASMA_X4");
			Stop;
	}
}