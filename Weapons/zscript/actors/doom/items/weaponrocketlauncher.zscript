Class RebalancedRocketLauncher : DoomWeapon replaces RocketLauncher
{
	Default
	{
		Weapon.SelectionOrder 600;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 4; // Give more starting ammo to make it more useful immediately
		Weapon.AmmoType "RocketAmm0";
		Weapon.SlotNumber 5;
		+WEAPON.NOAUTOFIRE;
		Inventory.PickupMessage "$GOTLAUNCHER";
		Tag "$TAG_ROCKETLAUNCHER";
	}
	States
	{
		Ready:
			MISG A 1 A_WeaponReady;
			Loop;
		Deselect:
			MISG A 1 A_Lower;
			Loop;
		Select:
			MISG A 1 A_Raise;
			Loop;
		Fire:
			MISG B 8 A_GunFlash;
			MISG B 8 {
				A_FireProjectile ("PlayerRocket"); // Slightly slower to make it similar fire rate to Cyberdemon, but deals twice as much AoE damage.
				A_Quake (2.5, 12, 0, 1, "");
				A_ZoomFactor (0.95);
			}
			MISG B 8 A_ZoomFactor (1.0);
			MISG B 0 A_ReFire;
			Goto Ready;
		Flash:
			MISF A 3 Bright A_Light1;
			MISF B 4 Bright;
			MISF CD 4 Bright A_Light2;
			Goto LightDone;
		Spawn:
			LAUN A -1;
			Stop;
	}
}

Class PlayerRocket : Rocket
{
	Default
	{
		DamageFunction (100); // No more random damage
		+EXTREMEDEATH; // Always gib
		Decal "Scorch";
		Speed 24; // 20% faster
		Radius 8; // Slightly smaller to let it hit enemies closer to the center
	}
	
	States
	{
		Death:
			MISL B 8 Bright {
				A_Explode (128, 128, XF_HURTSOURCE|XF_THRUSTZ|XF_CIRCULAR, damagetype:"Explosive"); // Make it a circular shape
				A_Explode (128, 128, XF_CIRCULARTHRUST|XF_CIRCULAR, damagetype:"Explosive"); // Don't hurt ourself more since we do more AoE damage.
				A_Quake (2.0, 8, 0, 128, "");
			}
			MISL C 6 Bright;
			MISL D 4 Bright;
			Stop;
	}
}



Class RocketAmm0 : Ammo replaces RocketAmmo
{
	Default
	{
		Inventory.PickupMessage "$GOTROCKETS";
		Inventory.Amount 2;
		Inventory.MaxAmount 50;
		Ammo.BackpackAmount 4;
		Ammo.BackpackMaxAmount 100;
		Inventory.Icon "ROCKA0";
		Tag "$AMMO_ROCKETS";
	}

	States
	{
		Spawn:
			ROCK B -1;
			Stop;
	}
}

Class RocketB0x : RocketAmm0 replaces RocketBox
{
	Default
	{
		Inventory.PickupMessage "$GOTROCKBOX";
		Inventory.Amount 10;
	}
	
	States
	{
		Spawn:
			BROK A -1;
			Stop;
	}
}