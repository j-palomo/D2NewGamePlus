Class RebalancedBFG9000 : BFG9000 replaces BFG9000
{
	Default
	{
		Weapon.SelectionOrder 300;
		-WEAPON.NOAUTOFIRE;
		Inventory.PickupMessage "$GOTBFG9000";
		Tag "$TAG_BFG9000";
		Weapon.SlotNumber 7;
	}
	
	States
	{
		Fire:
			BFGG A 0 {
				A_StartSound ("weapons/bfgf");
				A_Quake (1.5, 10, 0, 1, "");
				A_WeaponOffset (0, 32, WOF_INTERPOLATE);
			}
			BFGG A 2 A_WeaponOffset (Random (-1, 1), Random (32, 33), WOF_INTERPOLATE);
			BFGG A 2 A_WeaponOffset (Random (-1, 1), Random (32, 34), WOF_INTERPOLATE);
			BFGG A 2 A_WeaponOffset (Random (-2, 2), Random (32, 34), WOF_INTERPOLATE);
			BFGG A 2 A_WeaponOffset (Random (-2, 2), Random (32, 35), WOF_INTERPOLATE);
			BFGG A 2 A_WeaponOffset (Random (-3, 3), Random (32, 35), WOF_INTERPOLATE);
			BFGG A 0 A_Quake (2.0, 10, 0, 1, "");
			BFGG A 2 A_WeaponOffset (Random (-3, 3), Random (32, 36), WOF_INTERPOLATE);
			BFGG A 2 A_WeaponOffset (Random (-4, 4), Random (32, 36), WOF_INTERPOLATE);
			BFGG A 2 A_WeaponOffset (Random (-4, 4), Random (32, 37), WOF_INTERPOLATE);
			BFGG A 2 A_WeaponOffset (Random (-5, 5), Random (32, 37), WOF_INTERPOLATE);
			BFGG A 2 A_WeaponOffset (Random (-5, 5), Random (32, 38), WOF_INTERPOLATE);
			BFGG B 2 {
				A_GunFlash ();
				A_WeaponOffset (Random (-5, 5), Random (32, 38), WOF_INTERPOLATE);
				A_Quake (2.5, 10, 0, 1, "");
			}
			BFGG B 2 A_WeaponOffset (Random (-6, 6), Random (32, 38), WOF_INTERPOLATE);
			BFGG B 2 A_WeaponOffset (Random (-6, 6), Random (32, 39), WOF_INTERPOLATE);
			BFGG B 2 A_WeaponOffset (Random (-7, 7), Random (32, 49), WOF_INTERPOLATE);
			BFGG B 2 A_WeaponOffset (Random (-7, 7), Random (32, 40), WOF_INTERPOLATE);
			BFGG B 0 A_WeaponOffset (0, 32, WOF_KEEPY);
			BFGG B 2 {
				A_FireBFG ();
				A_WeaponOffset (0, 8, WOF_ADD|WOF_INTERPOLATE);
				A_ZoomFactor (0.94);
			}
			BFGG B 2 A_WeaponOffset (0, 6, WOF_ADD|WOF_INTERPOLATE);
			BFGG B 2 {
				A_WeaponOffset (0, 2, WOF_ADD|WOF_INTERPOLATE);
				A_ZoomFactor (0.96);
			}
			BFGG B 2 {
				A_WeaponOffset (0, -4, WOF_ADD|WOF_INTERPOLATE);
				A_ZoomFactor (0.98);
			}
			BFGG B 2 A_WeaponOffset (0, -6, WOF_ADD|WOF_INTERPOLATE);
			BFGG B 10 {
				A_ZoomFactor (1.0);
				A_WeaponOffset (0, 32, WOF_INTERPOLATE);
			}
			BFGG B 10 A_ReFire ();
			Goto Ready;
	}
}

Class RebalancedBFGBall : BFGBall replaces BFGBall
{
	Default
	{
		DamageFunction (400); // This should make it deadly at long ranges, even for bosses
		Decal "BFGLightning";
		+RollSprite;
		+RollCenter;
		+ForceXYBillboard;
		DamageType "Plasma";
	}
	
	States
	{
		Spawn:
			BFS1 AB 4 Bright Light ("BFGBALL") A_SetRoll (roll-32, SPF_INTERPOLATE);
			Loop;
		Death:
			---- A 0 Bright Light ("BFGBALL") A_SetRoll (0);
			BFE1 A 8 Bright Light ("BFGBALL_X1") {
				A_Explode(128, 256, XF_CIRCULAR|XF_CIRCULARTHRUST, false, 128, 0, 0, "", 'Plasma');
				A_Quake (1.5, 8, 0, 128, "");
			}
			BFE1 B 8 Bright Light ("BFGBALL_X2") {
				A_Explode(128, 256, XF_CIRCULAR|XF_CIRCULARTHRUST, false, 128, 0, 0, "", 'Plasma');
				A_Quake (2.5, 8, 0, 128, "");
			}
			BFE1 C 8 Bright Light ("BFGBALL_X3") {
				A_BFGSpray();
				A_Quake (3.5, 8, 0, 128, "");
			}
			BFE1 D 8 Bright Light ("BFGBALL_X1");
			BFE1 E 8 Bright Light ("BFGBALL_X4");
			BFE1 F 8 Bright Light ("BFGBALL_X5");
			Stop;
	}
}
