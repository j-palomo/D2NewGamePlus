Class Unarmed : DoomWeapon
{
	Default
	{
		Weapon.SlotNumber 1;
		Weapon.SelectionOrder 3700;
		Weapon.Kickback 100;
		Obituary "$OB_MPFIST";
		Tag "$TAG_FIST";
		+WEAPON.WIMPY_WEAPON;
		+WEAPON.MELEEWEAPON;
		+WEAPON.NOAUTOSWITCHTO;
		+WEAPON.CHEATNOTWEAPON;
		+INVENTORY.UNDROPPABLE;
		Decal "Fist";
	}
	
	States
	{
		Spawn:
			TNT1 A 1;
			Loop;
			
		Select:
			PUNG A Random(0, 1) A_Raise();
			Loop;
			
		Deselect:
			PUNG A Random(0, 1) A_Lower();
			Loop;
		
		Ready:
			PUNG A 1 A_WeaponReady ();
			Loop;
			
		Fire:
			PUNG A 1 A_WeaponOffset (15, 42, WOF_INTERPOLATE);
			PUNG A 1 A_WeaponOffset (35, 60, WOF_INTERPOLATE);
			Goto FireLoop;
		FireLoop:
			PUNG B 1 {
				A_WeaponOffset (-7, 42);
			}
			PUNG B 1 A_WeaponOffset (0, 32, WOF_INTERPOLATE);
			PUNG C 1 A_WeaponOffset (-3, 38, WOF_INTERPOLATE);
			PUNG C 1 A_WeaponOffset (-2, 36, WOF_INTERPOLATE);
			PUNG C 1 A_WeaponOffset (-1, 32, WOF_INTERPOLATE);
			PUNG C 1 A_WeaponOffset (0, 32, WOF_INTERPOLATE);
			PUNG D 1 {
			A_WeaponOffset (0, 32, WOF_INTERPOLATE);
			A_UnarmedAttack ();
			A_Recoil (-1.0 * cos(pitch));
			A_SetAngle (Angle-1.5, SPF_INTERPOLATE);
			}
			PUNG D 1 A_WeaponOffset (5, 36, WOF_INTERPOLATE);
			PUNG D 1 A_WeaponOffset (20, 41, WOF_INTERPOLATE);
			PUNG D 1 A_WeaponOffset (50, 49, WOF_INTERPOLATE);
			PUNG D 1 A_WeaponOffset (65, 63, WOF_INTERPOLATE);
			PUNG D 1 A_WeaponOffset (80, 81, WOF_INTERPOLATE);
			PUNG D 1 A_WeaponOffset (90, 90, WOF_INTERPOLATE);
			PUNG D 1 A_WeaponOffset (99, 99, WOF_INTERPOLATE);
			PUNG D 1 {
				A_WeaponOffset (106, 110, WOF_INTERPOLATE);
				A_SetAngle (Angle+1.5, SPF_INTERPOLATE);
			}
			PUNG B 0 A_WeaponOffset (0, 32, WOF_INTERPOLATE);
			PUNG B 5 A_ReFire ("FireLoop");
			Goto Ready;
	}
	
	Action void A_UnarmedAttack ()
	{
		int dmg = random(5,15);
		if (FindInventory("PowerStrength"))
		{
			dmg *= 10;
		}
		A_CustomPunch(dmg, true, pufftype:"UnarmedPuff", range: 96.0, meleesound: "*fist");
	}
}

Class UnarmedPuff : Actor
{
	Default
	{
		DamageType "Blunt";
		+NOBLOCKMAP;
		+NOGRAVITY;
		+ALLOWPARTICLES;
		+RANDOMIZE;
		+ZDOOMTRANS;
		RenderStyle "Translucent";
		Alpha 0.5;
		VSpeed 1;
		Mass 5;
	}
	
	States
	{
		Spawn:
			PUFF CD 4;
			Stop;
	}
}